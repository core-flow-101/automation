plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.7'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Testcontainers core
    testImplementation 'org.testcontainers:testcontainers:1.19.0'

    // Testcontainers JUnit 5 интеграция
    testImplementation 'org.testcontainers:junit-jupiter:1.19.0'

    // Если используешь PostgreSQL
    testImplementation 'org.testcontainers:postgresql:1.19.0'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'

    runtimeOnly 'org.postgresql:postgresql:42.6.0'
}

checkstyle {
    toolVersion = '10.12.1'
    configFile = file("${rootDir}/checkstyle.xml")
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}


spotbugs {
    toolVersion = '4.8.3'

    effort = com.github.spotbugs.snom.Effort.valueOf('MAX')
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf('LOW')
}


tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        xml.required = false
        html.required = true
    }
}

tasks.named('spotbugsMain') {
    ignoreFailures = true
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('test') {
    useJUnitPlatform()
}
